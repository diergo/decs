import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java-library'
    id 'com.github.spotbugs' version '5.0.13'
    id 'jacoco'
    id 'maven-publish'
}

ext {
    junitVersion = '5.9.1'
}

version = "${majorVersion}.${minorVersion}.${patchVersion}"

// dependencies

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
        "org.junit.jupiter:junit-jupiter-params:${junitVersion}",
        'org.hamcrest:hamcrest:2.2',
        'org.mockito:mockito-core:4.10.0'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

// compiling

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    configure(options) {
        encoding = 'UTF-8'
        deprecation = true
        compilerArgs << "-Xlint:unchecked"
    }
}

// unit tests

test {
    useJUnitPlatform {
        excludeTags 'performance'
    }
    reports {
        html.required = true
    }
}

// publishing

javadoc {
    options {
        memberLevel = JavadocMemberLevel.PROTECTED
        author = false
        header "${project.description} ${project.version}"
        links 'https://docs.oracle.com/javase/8/docs/api/'
        encoding 'UTF-8'
        addStringOption('Xdoclint:none', '-quiet')
    }
}

tasks.withType(Jar).configureEach {
    from(project.projectDir) {
        include 'LICENSE'
        into('META-INF')
    }
    manifest {
        attributes(
            "Automatic-Module-Name": "diergo.csv",
            "Specification-Title": project.description,
            "Specification-Version": "${majorVersion}.${minorVersion}",
            "Specification-Vendor": vendor,
            "Implementation-Title": project.name,
            "Implementation-Version": "${majorVersion}.${minorVersion}.${patchVersion}",
            "Implementation-Vendor": vendor,
            "Implementation-Vendor-Id": project.group
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                description = project.description
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

// code analysis

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'CLASS'
                minimum = 0.9
            }
            limit {
                counter = 'LINE'
                minimum = 0.9
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.8
            }
        }
    }
}

// performance test

tasks.register('performance', Test) {
    group JavaBasePlugin.VERIFICATION_GROUP
    description 'Runs the performance test suite.'
    useJUnitPlatform {
        includeTags 'performance'
    }
    reports {
        html.required = true
    }
}

tasks.register('downloadTestData') {
    ext.localCopy = file('src/test/resources/worldcities.csv')
    ext.tempFile = file('build/resources/worldcities.zip')
    outputs.file localCopy
    doLast {
        println "Loading test data from https://simplemaps.com/data/world-cities"
        ant {
            get(
                    src: "https://simplemaps.com/static/data/world-cities/basic/simplemaps_worldcities_basicv1.73.zip",
                    dest: tempFile,
            )
        }
        copy {
            from(zipTree(tempFile)) {
                include '*.csv'
            }
            into localCopy.parentFile
        }
    }
}

// task dependencies

check.dependsOn jacocoTestCoverageVerification, jacocoTestReport
assemble.dependsOn javadoc
performance.dependsOn downloadTestData
