import com.github.spotbugs.snom.SpotBugsTask

plugins {
    id 'java-library'
    id 'com.github.spotbugs' version '4.7.0'
    id 'jacoco'
    id 'maven-publish'
}

ext {
    spotbugsVersion = '4.2.2'
    junitVersion = '5.7.1'
}

version = "${majorVersion}.${minorVersion}.${patchVersion}"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
        "org.junit.jupiter:junit-jupiter-params:${junitVersion}",
        'org.hamcrest:hamcrest-library:1.3',
        'org.mockito:mockito-core:3.8.0'

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
        excludeTags 'performance'
    }
    testLogging {
        events 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
}

task performance(type: Test) {
    useJUnitPlatform {
        includeEngines 'junit-jupiter'
        includeTags 'performance'
    }
    testLogging {
        events 'skipped', 'failed'
    }
    reports {
        html.enabled = true
    }
}

javadoc {
    options.memberLevel = 'PROTECTED'
    options.author = false
    options.header = "${project.description} ${project.version}"
    options.links(["http://docs.oracle.com/javase/8/docs/api/"] as String[])
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
}

tasks.withType(Jar) {
    manifest {
        attributes(
            "Automatic-Module-Name": "diergo.decs",
            "Specification-Title": project.description,
            "Specification-Version": "${majorVersion}.${minorVersion}",
            "Specification-Vendor": vendor,
            "Implementation-Title": project.name,
            "Implementation-Version": "${majorVersion}.${minorVersion}.${patchVersion}",
            "Implementation-Vendor": vendor,
            "Implementation-Vendor-Id": project.group
        )
    }
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacoco {
    toolVersion = '0.8.6'
}

spotbugs {
    toolVersion = spotbugsVersion
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'CLASS'
                minimum = 0.9
            }
            limit {
                counter = 'LINE'
                minimum = 0.9
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.8
            }
        }
    }
}

task downloadTestData {
    ext.localCopy = file('src/test/resources/worldcities.csv')
    ext.tempFile = file('build/resources/worldcities.zip')
    outputs.file localCopy
    doLast {
        println "Loading test data from https://simplemaps.com/data/world-cities"
        ant {
            get(
                    src: "https://simplemaps.com/static/data/world-cities/basic/simplemaps_worldcities_basicv1.73.zip",
                    dest: tempFile,
            )
        }
        copy {
            from(zipTree(tempFile)) {
                include '*.csv'
            }
            into localCopy.parentFile
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                description = project.description
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification, jacocoTestReport
assemble.dependsOn javadoc
performance.dependsOn downloadTestData

wrapper {
    gradleVersion = '7.6'
}
